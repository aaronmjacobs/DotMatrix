### Project ###

cmake_minimum_required(VERSION 3.17)
project(DotMatrixCore VERSION 0.0.0.1 LANGUAGES CXX C)
set(PROJECT_DISPLAY_NAME "Dot Matrix")

# Options
option(DOT_MATRIX_ENABLE_BOOTSTRAP "Enable bootstrap" OFF)
option(DOT_MATRIX_ENABLE_DEBUGGER "Enable debugger" OFF)
option(DOT_MATRIX_ENABLE_PER_CHANNEL_AUDIO_BUFFERS "Enable per-channel audio buffers" OFF)
option(DOT_MATRIX_WITH_LIBRETRO "Enable libretro support" OFF)

if(${DOT_MATRIX_WITH_LIBRETRO})
   set(DOT_MATRIX_WITH_BOOTSTRAP OFF)
   set(DOT_MATRIX_WITH_DEBUGGER OFF)
   set(DOT_MATRIX_WITH_DEBUGGER OFF PARENT_SCOPE)
   set(DOT_MATRIX_WITH_PER_CHANNEL_AUDIO_BUFFERS OFF)
else()
   set(DOT_MATRIX_WITH_BOOTSTRAP ${DOT_MATRIX_ENABLE_BOOTSTRAP})
   set(DOT_MATRIX_WITH_DEBUGGER ${DOT_MATRIX_ENABLE_DEBUGGER})
   set(DOT_MATRIX_WITH_DEBUGGER ${DOT_MATRIX_ENABLE_DEBUGGER} PARENT_SCOPE)
   set(DOT_MATRIX_WITH_PER_CHANNEL_AUDIO_BUFFERS ${DOT_MATRIX_ENABLE_PER_CHANNEL_AUDIO_BUFFERS})
endif()

### Target ###

if(${DOT_MATRIX_WITH_LIBRETRO})
   add_library(${PROJECT_NAME} SHARED "")
else()
   add_library(${PROJECT_NAME} STATIC "")
endif()

target_compile_features(${PROJECT_NAME} PUBLIC cxx_std_17)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_DEBUG=$<CONFIG:Debug>)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_WITH_BOOTSTRAP=$<BOOL:${DOT_MATRIX_WITH_BOOTSTRAP}>)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_WITH_DEBUGGER=$<BOOL:${DOT_MATRIX_WITH_DEBUGGER}>)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_WITH_PER_CHANNEL_AUDIO_BUFFERS=$<BOOL:${DOT_MATRIX_WITH_PER_CHANNEL_AUDIO_BUFFERS}>)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_PROJECT_DISPLAY_NAME="${PROJECT_DISPLAY_NAME}")
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_PROJECT_VERSION_STRING="${PROJECT_VERSION}")

include(TestBigEndian)
TEST_BIG_ENDIAN(DOT_MATRIX_IS_BIG_ENDIAN)
target_compile_definitions(${PROJECT_NAME} PUBLIC DM_IS_BIG_ENDIAN=$<BOOL:${DOT_MATRIX_IS_BIG_ENDIAN}>)

if(WIN32)
   target_compile_definitions(${PROJECT_NAME} PRIVATE NOMINMAX)
endif()

set_property(DIRECTORY ${CMAKE_PROJECT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})

### Source ###

# Source files
include(Source.cmake)

# Libraries
include(Libraries.cmake)
